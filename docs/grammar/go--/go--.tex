
\begin{grammar}

    <MSourceFile>       ::=     <PackageClause> ";" [ <TopLevelDecl_loop> ]

    <TopLevelDecl_loop> ::=     <TopLevelDecl> ";" [ <TopLevelDecl_loop> ]

    <PackageClause>     ::=     "package" <PackageName>

    <PackageName>       ::=     <identifier>

    <Block>             ::=     "{" <StatementList> "}"

    <StatementList>     ::=     <Statement> ";" [ <StatementList> ]

    <Statement>             ::=     <Declaration> | <LabeledStmt> | <SimpleStmt>
                            |    <ReturnStmt> | <BreakStmt> | <ContinueStmt> 
                            |    <GotoStmt> | <FallthroughStmt> | <Block> | <IfStmt>
                            |    <SwitchStmt> | <ForStmt> 

    <SimpleStmt>            ::=     <EmptyStmt> | <ExpressionStmt> | <SendStmt>
                            |    <IncDecStmt> | <Assignment> | <ShortVarDecl>

    <EmptyStmt>             ::=     ""

    <LabeledStmt>           ::=     <Label> ":" <Statement>

    <Label>                 ::=     <identifier>

    <ExpressionStmt>        ::=     <Expression>

    <IncDecStmt>            ::=     <Expression> ( "++" | "--" )

    <Assignment>            ::=     <ExpressionList> <assign_op> <ExpressionList>

    <assign_op>             ::=     [ <add_op> | <mul_op> ] "="

    <IfStmt>                ::=     "if" [ <SimpleStmt> ";" ] <Expression> <Block> [ "else" ( <IfStmt> | <Block> ) ]

    <SwitchStmt>            ::=     <ExprSwitchStmt>
    <ExprSwitchStmt>        ::=     "switch" [ <SimpleStmt> ";" ] [ <Expression> ] "{" [ <ExprCaseClause_loop> ] "}"

    <ExprCaseClause_loop>   ::=     <ExprCaseClause> [ <ExprCaseClause_loop> ]

    <ExprCaseClause>        ::=     <ExprSwitchCase> ":" <StatementList>

    <ExprSwitchCase>        ::=     "case" <ExpressionList> | "default"

    <ForStmt>               ::=     "for" [ <Condition> | <ForClause> | <RangeClause> ] <Block>

    <Condition>             ::=     <Expression>

    <ForClause>             ::=     [ <InitStmt> ] ";" [ <Condition> ] ";" [ <PostStmt> ]

    <InitStmt>              ::=     <SimpleStmt>

    <PostStmt>              ::=     <SimpleStmt>

    <RangeClause>           ::=     [ <ExpressionList> "=" | <IdentifierList> ":=" ] "range" <Expression>

    <ReturnStmt>            ::=     "return" [ <ExpressionList> ]

    <BreakStmt>             ::=     "break" [ <Label> ]

    <ContinueStmt>          ::=     "continue" [ <Label> ]

    <GotoStmt>              ::=     "goto" <Label>

    <FallthroughStmt>       ::=     "fallthrough"

    <Operand>           ::= <Literal> | <OperandName> | <MethodExpr> | "(" <Expression> ")"

    <Literal>           ::= <BasicLit> | <CompositeLit> | <FunctionLit>

    <BasicLit>          ::= <int_lit> | <float_lit> | <imaginary_lit> | <rune_lit> | <string_lit>

    <OperandName>       ::= <identifier>

    <CompositeLit>      ::=     <LiteralType> <LiteralValue>

    <LiteralType>       ::=     <StructType> | <ArrayType> | "[" "..." "]" <ElementType>
                        |    <SliceType> | <TypeName>

    <LiteralValue>      ::=     "{" [ <ElementList> [ "," ] ] "}"

    <ElementList>       ::=     <KeyedElement> [ ", " <ElementList> ]

    <KeyedElement>      ::=     [ <Key> ":" ] <Element>

    <Key>               ::=     <FieldName> | <Expression> | <LiteralValue>

    <FieldName>         ::=     <identifier>

    <Element>           ::=     <Expression> | <LiteralValue>

    <FunctionLit>       ::= "func" <Function>

    <PrimaryExpr>       ::=     <Operand>
                        |       <Conversion>
                        |       <PrimaryExpr> <Selector>
                        |       <PrimaryExpr> <Index>
                        |       <PrimaryExpr> <Slice>
                        |       <PrimaryExpr> <TypeAssertion>
                        |       <PrimaryExpr> <Arguments>

    <Selector>          ::=     "." <identifier>

    <Index>             ::=     "[" <Expression> "]"

    <Slice>             ::=     "[" [ <Expression> ] ":" [ <Expression> ] "]"
                        |       "[" [ <Expression> ] ":" <Expression> ":" <Expression> "]"

    <TypeAssertion>     ::=     "." "(" <Type> ")"

    <Arguments>         ::=     "(" [ ( <ExpressionList> | <Type> [ "," <ExpressionList> ] ) [ "..." ] [ "," ] ] ")"

    <MethodExpr>        ::=     <ReceiverType> "." <MethodName>

    <ReceiverType>      ::=     <TypeName> | "(" "*" <TypeName> ")" | "(" <ReceiverType> ")"

    <Expression>        ::=     <UnaryExpr> | <Expression> <binary_op> <Expression>

    <UnaryExpr>         ::=     <PrimaryExpr> | <unary_op> <UnaryExpr>

    <binary_op>         ::=     "||" | "&&" | <rel_op> | <add_op> | <mul_op>

    <rel_op>            ::=     "==" | "!=" | "<" | "<=" | "\>" | "\>="

    <add_op>            ::=     "+" | "-" | "|" | "^"

    <mul_op>            ::=     "*" | "/" | "%" | "<<" | ">>" | "&" | "&^"

    <unary_op>          ::=     "+" | "-" | "!" | "^" | "*" | "&" | "<-"

    <Conversion>        ::=     <Type> "(" <Expression> [ "," ] ")"


    <Declaration>       ::=     <ConstDecl> | <TypeDecl> | <VarDecl>

    <TopLevelDecl>      ::=     <Declaration> | <FunctionDecl> | <MethodDecl>

    <ConstDecl>         ::=     "const" ( <ConstSpec> | "(" [ <ConstSpec_loop> ] ")" )

    <ConstSpec_loop>    ::=     <ConstSpec> ";" [ <ConstSpec_loop> ]

    <ConstSpec>         ::=     <IdentifierList> [ [ <Type> ] "=" <ExpressionList> ]

    <IdentifierList>    ::=     <identifier> [ "," <IdentifierList> ]


    <ExpressionList>    ::=     <Expression> [ "," <ExpressionList> ]

    <TypeDecl>          ::=     "type" ( <TypeSpec> | "(" [ <TypeSpec_loop> ] ")" )

    <TypeSpec_loop>     ::=     <TypeSpec> ";" [ <TypeSpec_loop> ]

    <TypeSpec>          ::=     <identifier> <Type>

    <VarDecl>           ::=     "var" ( <VarSpec> | "(" [ <VarSpec_loop> ] ")" )

    <VarSpec_loop>      ::=     <VarSpec> ";" [ <VarSpec_loop> ]

    <VarSpec>           ::=     <IdentifierList> ( <Type> [ "=" <ExpressionList> ] | "=" <ExpressionList> )

    <ShortVarDecl>      ::=     <IdentifierList> ":=" <ExpressionList>

    <FunctionDecl>      ::=     "func" <FunctionName> ( <Function> | <Signature> )

    <FunctionName>      ::=     <identifier>

    <Function>          ::=     <Signature> <FunctionBody>

    <FunctionBody>      ::=     <Block>

    <MethodDecl>          ::=     "func" <Receiver> <MethodName> ( <Function> | <Signature> )

    <Receiver>            ::=     <Parameters>


    <Type>              ::=     <TypeName> | <TypeLit> | "(" <Type> ")"

    <TypeName>          ::=     <identifier> 

    <TypeLit>           ::=     <ArrayType> | <StructType> 
                        |    <FunctionType> | <SliceType>
                        |    <BuiltinType>

    <MethodName>        ::=     <identifier>

    <FunctionType>      ::=     "func" <Signature>

    <Signature>         ::=     <Parameters> [ <Result> ]

    <Result>            ::=     <Parameters> | <Type>

    <Parameters>        ::=     "(" [ <ParameterList> [ "," ] ] ")"

    <ParameterList>     ::=     <ParameterDecl> [ "," <ParameterList> ]

    <ParameterDecl>     ::=     [ <IdentifierList> ] [ "..." ] <Type>

    <StructType>        ::=     "struct" "{" [ <FieldDecl_loop> ] "}"

    <FieldDecl_loop>    ::=     <FieldDecl> ";" [ <FieldDecl_loop> ]

    <FieldDecl>         ::=     (<IdentifierList> <Type> | <AnonymousField>) [ <Tag> ]

    <AnonymousField>    ::=     [ "*" ] <TypeName>

    <Tag>               ::=     <string_lit>

    <SliceType>         ::=     "[" "]" <ElementType>

    <ArrayType>         ::=     "[" <ArrayLength> "]" <ElementType>

    <ArrayLength>       ::=     <Expression>

    <ElementType>       ::=     <Type>

    <BuiltinType>           ::=     "bool" | "int" | "float32" | "string" | "rune"

    <constants>         ::=     "true" | "false" | "iota"

    <zero_val>          ::=     "nil" 

    <identifier>    ::= <letter> <ident_rem>

    <ident_rem>     ::= ( <letter> | <unicode_letter> ) [ <ident_rem> ]

    <string_lit>        ::=     <raw_string_lit> | <inter_string_lit>

    <raw_string_lit>    ::=     "`" [ <raw_loop> ] "'"

    <raw_loop>          ::=     ( <unicode_char> | "newline" ) [ <raw_loop> ] 

    <inter_string_lit>  ::=     "\"" [ <inter_loop> ] "\""

    <inter_loop>        ::=     ( <unicode_value> | <byte_value> ) [ <inter_loop> ]

    <imaginary_lit>     ::=     ( <decimals> | <float_lit> ) "i"

    <float_lit>         ::=     <decimals> "." [ <decimals> ] [ <exponent> ]
                        |    <decimals> <exponent>
                        |    "." <decimals> [ <exponent> ]

    <decimals>          ::=     <decimal_digit> [ <decimals> ]

    <exponent>          ::=     ( "e" | "E" ) [ "+" | "-" ] <decimals>

    <int_lit>           ::=     <decimal_lit> | <octal_lit> | <hex_lit>

    <decimal_lit>       ::=     ( "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ) [ <dec_lit_loop> ]

    <dec_lit_loop>      ::=     <decimal_digit> [ <dec_lit_loop> ]

    <octal_lit>         ::=     "0" [ <oct_lit_loop> ]

    <oct_lit_loop>      ::=     <octal_digit> [ <oct_lit_loop> ]

    <hex_lit>           ::=     "0" ( "x" | "X" ) <hex_digit> [ <hex_lit_loop> ]

    <hex_lit_loop>      ::=     <hex_digit> [ <hex_lit_loop> ]

    <letter>            ::=     <unicode_letter> | "_"

    <decimal_digit>     ::=     <octal_digit> | "8" | "9"

    <octal_digit>       ::=     "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7"

    <hex_digit>         ::=     <decimal_digit> | "A" | "B" | "C" | "D" | "E" | "F" | "a" | "b" | "c" | "d" | "e" | "f"

    <rune_lit>          ::=     "`" ( <unicode_value> | <byte_value> ) "\""

    <unicode_value>     ::=     <unicode_char> | <little_u_value> | <big_u_value> | <escaped_char>

    <byte_value>        ::=     <octal_byte_value> | <hex_byte_value>

    <octal_byte_value>  ::=     "\\" <octal_digit> <octal_digit> <octal_digit>

    <hex_byte_value>    ::=     "\\" "x" <hex_digit> <hex_digit>

    <little_u_value>    ::=     "\\" "u" <hex_digit> <hex_digit> <hex_digit> <hex_digit>

    <big_u_value>       ::=     "\\" "U" <hex_digit> <hex_digit> <hex_digit> <hex_digit> <hex_digit> <hex_digit> <hex_digit> <hex_digit>

    <escaped_char>      ::=     "\\" ( "a" | "b" | "f" | "n" | "r" | "t" | "v" | "\\" | "\'" | "\"" )

\end{grammar}